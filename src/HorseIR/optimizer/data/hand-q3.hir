module default{
  import Builtin.*;
  def main() : table{
    a0:? = @load_table(`customer:sym);
    a1:? = @load_table(`orders:sym);
    a2:? = @load_table(`lineitem:sym);
    // customer
    c00:? = check_cast(@column_value(a0,`c_mktsegment:sym   ), sym );
    c01:? = check_cast(@column_value(a0,`c_custkey:sym      ), i64 );
    // orders
    c10:? = check_cast(@column_value(a1,`o_orderdate:sym    ), d   );
    c11:? = check_cast(@column_value(a1,`o_shippriority:sym ), i64 );
    c12:? = check_cast(@column_value(a1,`o_custkey:sym      ), enum); //i64
    c13:? = check_cast(@column_value(a1,`o_orderkey:sym     ), i64 );
    // lineitem
    c20:? = check_cast(@column_value(a2,`l_orderkey:sym     ), enum);
    c21:? = check_cast(@column_value(a2,`l_extendedprice:sym), f64 );
    c22:? = check_cast(@column_value(a2,`l_discount:sym     ), f64 );
    c23:? = check_cast(@column_value(a2,`l_shipdate:sym     ), d   );
    // custkey: c_ (key) and o_ (fkey)
    t0_0:? = @eq(c00, `"BUILDING":sym); // k_mask
    t0_1:? = @lt(c10, 1995.03.15:d);    // f_mask
    t0_2:? = @values(c12);
    t0_3:? = @compress(t0_1, t0_2);
    t0_4:? = @index(t0_0, t0_3);
    t0_5:? = @compress(t0_4, t0_3);  // key
    t0_6:? = @where(t0_1);
    t0_7:? = @compress(t0_4, t0_6);  // value
    t0_8:? = @index(c13, t0_7);
    t0_9:? = @index(c10, t0_7);
    t0_10:? = @index(c11, t0_7);
    // orderkey: o_ and l_
    t1_0:? = @gt(c23, 1995.03.15:d);
    t1_1:? = @compress(t1_0, c20);
    t1_2:? = @fetch(t1_1);
    t1_3:? = @join_index(@eq, t0_8, t1_2);
    t1_4:? = @index(t1_3, 0:i64);
    t1_5:? = @index(t1_3, 1:i64);
    t1_6:? = @index(t1_2, t1_5 ); // l_orderkey
    t1_7:? = @index(t0_9, t1_4 ); // o_orderdate
    t1_8:? = @index(t0_10, t1_4 );// o_shippriority
    t1_9:? = @compress(t1_0, c21);
    t1_10:? = @compress(t1_0, c22);
    t1_11:? = @index(t1_9, t1_5);  // l_extendedprice
    t1_12:? = @index(t1_10, t1_5); // l_discount
    // materialize
    t2_0:? = @minus(1:i64, t1_12);
    t2_1:? = @mul(t1_11, t2_0);    // revenue
    // group
    t3_0:? = @list(t1_6, t1_7, t1_8);
    t3_1:? = @group(t3_0);
    t3_2:? = @keys(t3_1);
    t3_3:? = @values(t3_1);
    t3_4:? = @each_right(@index,t2_1, t3_3);
    t3_5:? = @each(@sum, t3_4);
    t3_6:? = @raze(t3_5);  // revenue
    t3_7:? = @index(t1_6, t3_2); // l_orderkey
    t3_8:? = @index(t1_7, t3_2); // o_orderdate
    t3_9:? = @index(t1_8, t3_2); // o_shippriority
    // order by
    t4_0:? = @list(t3_6, t3_8);
    t4_1:? = @order(t4_0, (0, 1):bool);
    t4_2:? = @index(t3_7, t4_1);
    t4_3:? = @index(t3_6, t4_1);
    t4_4:? = @index(t3_8, t4_1);
    t4_5:? = @index(t3_9, t4_1);
    // return debug
    w0:? = `l_orderkey`revenue`o_orderdate`o_shippriority:sym;
    w1:? = @list(t4_2, t4_3, t4_4, t4_5);
    w2:? = @table(w0, w1);
    return w2;
  }
}

