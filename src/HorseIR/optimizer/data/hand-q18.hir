module default{
  import Builtin.*;
  def main() : table{
      a0:? = @load_table(`customer:sym);
      a1:? = @load_table(`orders:sym);
      a2:? = @load_table(`lineitem:sym);
      // customer
      c00:? = check_cast(@column_value(a0, `c_custkey:sym  ), i64);
      c01:? = check_cast(@column_value(a0, `c_name:sym     ), sym);
      // orders
      c10:? = check_cast(@column_value(a1, `o_orderkey:sym  ), i64);
      c11:? = check_cast(@column_value(a1, `o_orderdate:sym ), d  );
      c12:? = check_cast(@column_value(a1, `o_totalprice:sym), f64);
      c13:? = check_cast(@column_value(a1, `o_custkey:sym   ), enum);
      // lineitem
      c20:? = check_cast(@column_value(a2, `l_orderkey:sym  ), enum);
      c21:? = check_cast(@column_value(a2, `l_quantity:sym  ), i64);
      // 0. group by
      t0_k:? = @fetch(c20);
      t0_0:? = @group(t0_k);
      t0_1:? = @keys(t0_0);
      t0_2:? = @values(t0_0);
      t0_3:? = @each_right(@index, c21, t0_2);
      t0_4:? = @each(@sum, t0_3);
      t0_5:? = @raze(t0_4);
      t0_6:? = @gt(t0_5, 300:i32);
      t0_7:? = @compress(t0_6, t0_1);
      t0_8:? = @index(t0_k, t0_7);
      // 1. orderkey: 0_ and o_
      t1_0:? = @member(t0_8, c10);
      t1_1:? = @values(c13);
      // 2. custkey: c_ and 1_
      // 3. orderkey: 2_ and l_
      t3_0:? = @values(c20);
      t3_1:? = @index(t1_0, t3_0);    // mask : l_
      t3_2:? = @compress(t3_1, t3_0); // index: o_
      t3_3:? = @index(t1_1, t3_2);    // index: c_
      t3_4:? = @index(c00, t3_3); // c_custkey
      t3_5:? = @index(c01, t3_3); // c_name
      t3_6:? = @index(c10, t3_2); // o_orderkey
      t3_7:? = @index(c11, t3_2); // o_orderdate
      t3_8:? = @index(c12, t3_2); // o_totalprice
      t3_9:? = @compress(t3_1, c21); // l_quantity
      // 4. group
      t4_0:? = @list(t3_5, t3_4, t3_6, t3_7, t3_8);
      t4_1:? = @group(t4_0);
      t4_2:? = @keys(t4_1);
      t4_3:? = @values(t4_1);
      t4_4:? = @each_right(@index, t3_9, t4_3);
      t4_5:? = @each(@sum, t4_4);
      t4_6:? = @raze(t4_5);
      t4_7:? = @index(t3_5, t4_2);
      t4_8:? = @index(t3_4, t4_2);
      t4_9:? = @index(t3_6, t4_2);
      t4_10:? = @index(t3_7, t4_2);
      t4_11:? = @index(t3_8, t4_2);
      // 5. order by
      t5_0:? = @list(t4_11, t4_10);
      t5_1:? = @order(t5_0, (0,1):bool);
      t5_2:? = @index(t4_7, t5_1);
      t5_3:? = @index(t4_8, t5_1);
      t5_4:? = @index(t4_9, t5_1);
      t5_5:? = @index(t4_10, t5_1);
      t5_6:? = @index(t4_11, t5_1);
      t5_7:? = @index(t4_6, t5_1);
      // result
      w0:? = `c_name`c_custkey`o_orderkey`o_orderdate`o_totalprice`sum:sym;
      w1:? = @list(t5_2,t5_3,t5_4,t5_5,t5_6,t5_7);
      w2:? = @table(w0,w1);
      return w2;
  }
}
