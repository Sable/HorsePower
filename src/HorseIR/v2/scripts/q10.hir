module default{
  import Builtin.*;
  def main() : table{
      a0:? = @load_table(`customer:sym);
      a1:? = @load_table(`orders:sym);
      a2:? = @load_table(`lineitem:sym);
      a3:? = @load_table(`nation:sym);
      // customer
      c00:? = check_cast(@column_value(a0, `c_custkey:sym  ), i64);
      c01:? = check_cast(@column_value(a0, `c_nationkey:sym), enum);
      c02:? = check_cast(@column_value(a0, `c_name:sym     ), sym);
      c03:? = check_cast(@column_value(a0, `c_acctbal:sym  ), i64);
      c04:? = check_cast(@column_value(a0, `c_phone:sym    ), str);
      c05:? = check_cast(@column_value(a0, `c_address:sym  ), str);
      c06:? = check_cast(@column_value(a0, `c_comment:sym  ), str);
      // orders
      c10:? = check_cast(@column_value(a1, `o_custkey:sym  ), enum);
      c11:? = check_cast(@column_value(a1, `o_orderkey:sym ), i64);
      c12:? = check_cast(@column_value(a1, `o_orderdate:sym), d  );
      // lineitem
      c20:? = check_cast(@column_value(a2, `l_orderkey:sym     ), enum);
      c21:? = check_cast(@column_value(a2, `l_returnflag:sym   ), char);
      c22:? = check_cast(@column_value(a2, `l_extendedprice:sym), f64);
      c23:? = check_cast(@column_value(a2, `l_discount:sym     ), f64);
      // nation
      c30:? = check_cast(@column_value(a3, `n_nationkey:sym ), i64);
      c31:? = check_cast(@column_value(a3, `n_name:sym      ), sym);
      // 0. custkey: o_ and c_
      t0_0:? = @geq(c12, 1993-10-01:d);
      t0_1:? = @lt(c12, 1994-01-01:d);
      t0_2:? = @and(t0_0, t0_1);
      t0_3:? = @values(c10);
      //t0_4:? = @compress(t0_2, t0_3); // customer
      // 1. nationkey: n_ and 0_
      t1_0:? = @values(c01);
      //t1_1:? = @index(t1_0, t0_4);    // nation
      // 2. orderkey: 1_ (k_mask: t0_2) and l_ (f_mask: t2_0)
      t2_0:? = @eq(c21, 'R':char);    // lineitem
      t2_1:? = @values(c20);
      t2_2:? = @compress(t2_0, t2_1);
      t2_3:? = @index(t0_2, t2_2);
      t2_4:? = @compress(t2_3, t2_2);  // p0
      t2_5:? = @where(t2_0);
      t2_6:? = @compress(t2_3, t2_5);  // p1: indexing
      // 3. values
      t3_0:? = @index(t0_3, t2_4);
      t3_1:? = @index(c00, t3_0);
      t3_2:? = @index(c02, t3_0);
      t3_3:? = @index(c03, t3_0);
      t3_4:? = @index(c04, t3_0);
      t3_5:? = @index(c05, t3_0);
      t3_6:? = @index(c06, t3_0);
      t3_7:? = @index(t1_0, t3_0);
      t3_8:? = @index(c31, t3_7);  // n_name
      t3_9:? = @index(c22, t2_6);  // l_extendedprice
      t3_10:? = @index(c23, t2_6); // l_discount
      // 4. group by
      t4_0:? = @list(t3_1, t3_2, t3_3, t3_4, t3_8, t3_5, t3_6);
      t4_1:? = @group(t4_0);
      t4_2:? = @keys(t4_1);
      t4_3:? = @values(t4_1);
      t4_4:? = @minus(1:i64, t3_10);
      t4_5:? = @mul(t3_9, t4_4);
      t4_6:? = @each_right(@index, t4_5, t4_3);
      t4_7:? = @each(@sum, t4_6);
      t4_8:? = @raze(t4_7);  // revenue
      t4_9:? = @index(t3_1, t4_2);  // c_custkey
      t4_10:? = @index(t3_2, t4_2); // c_name
      t4_11:? = @index(t3_3, t4_2); // c_acctbal
      t4_12:? = @index(t3_4, t4_2); // c_phone
      t4_13:? = @index(t3_5, t4_2); // c_address
      t4_14:? = @index(t3_6, t4_2); // c_comment
      t4_15:? = @index(t3_8, t4_2); // n_name
      // 5. order by
      t5_0:? = @order(t4_8 , 0:bool);
      t5_1:? = @index(t4_9 , t5_0);
      t5_2:? = @index(t4_10, t5_0);
      t5_3:? = @index(t4_8 , t5_0); // revenue
      t5_4:? = @index(t4_11, t5_0);
      t5_5:? = @index(t4_15, t5_0); // n_name
      t5_6:? = @index(t4_13, t5_0);
      t5_7:? = @index(t4_12, t5_0);
      t5_8:? = @index(t4_14, t5_0);
      // result
      w0:? = (`c_custkey,`c_name,`revenue,`c_acctbal,`n_name,`c_address,`c_phone,`c_comment):sym;
      w1:? = @list(t5_1, t5_2, t5_3, t5_4, t5_5, t5_6, t5_7, t5_8);
      w2:? = @table(w0,w1);
      return w2;
  }
}
