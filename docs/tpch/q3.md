# TPC-H Query 3

## SQL

```sql
  select
    l_orderkey,
    sum(l_extendedprice * (1 - l_discount)) as revenue,
    o_orderdate,
    o_shippriority
  from
    customer,
    orders,
    lineitem
  where
    c_mktsegment = 'BUILDING'
    and c_custkey = o_custkey
    and l_orderkey = o_orderkey
    and o_orderdate < date '1995-03-15'
    and l_shipdate > date '1995-03-15'
  group by
    l_orderkey,
    o_orderdate,
    o_shippriority
  order by
    revenue desc,
    o_orderdate;
```

## HorseIR

```
import Bultin;

module default{
    def main() : table{
        // load table
        a0:table     = @load_table(`customer);
        a1:table     = @load_table(`orders);
        a2:table     = @load_table(`lineitem);

        // select 1
        t0:d              = check_cast(@column_value(a1, `o_orderdate    ), d);
        t1:i64            = check_cast(@column_value(a1, `o_shippriority ), i64);
        t2:enum<i64,i64>  = check_cast(@column_value(a1, `o_custkey      ), enum<i64,i64>);
        w0:bool           = @lt(t0, 1995.03.15d);

        // select 2
        t3:sym            = check_cast(@column_value(a0, `c_mktsegment   ), sym);
        w1:bool           = @eq(t3, `BUILDING);

        // join 1
        w2:i64            = @values(t2);
        w3:bool           = @index(w1, w2);
        w4:bool           = @and(w0, w3);

        // select 3
        t4:d              = check_cast(@column_value(a2, `l_shipdate     ), d  );
        t5:enum<i64,i64>  = check_cast(@column_value(a2, `l_orderkey     ), enum<i64,i64>);
        t10:f64           = check_cast(@column_value(a2, `l_extendedprice), f64);
        t11:f64           = check_cast(@column_value(a2, `l_discount     ), f64);
        w5:bool           = @gt(w4, 1995.03.15d);

        // join 2
        w6:i64      = @values(t5);
        w7:bool     = @index(w4, w6);
        w8:bool     = @and(w5, w7);

        // ...
        w9:i64      = @compress(w8, w6);
        w10:i64     = @where(w8);

        // project 1
        p1:i64      = @keys(t5);
        p2:i64      = @index(p1, w9);
        p3:d        = @index(t0, w9);
        p4:i64      = @index(t1, w9);
        p5:f64      = @index(t10,w10);
        p6:f64      = @index(t11,w10);
        p7:f64      = @minus(1.0:f64, p6);
        p8:f64      = @mul(p5,p7);

        // group by
        g1:list<?>             = @list(p2,p3,p4);
        g2:list<i64,list<i64>> = @group(g1);
        g3:list<i64>           = @values(g2);
        g4:i64                 = @keys(g2);
        g5:list<f64>           = @each_right(@index, p8, g3);
        g6:list<f64>           = @each(@sum, g5);
        g7:i64                 = @index(p2,g4);
        g8:d                   = @index(p3,g4);
        g9:i64                 = @index(p4,g4);
        g10:f64                = @raze(g6);
       
        // order by
        d0:list<?> = @list(g10, g7); 
        d1:i64     = @order(d0, (0,1):bool);

        // materialization
        m0:i64     = @index(g7, d1);
        m1:f64     = @index(g10, d1);
        m2:d       = @index(g8, d1);
        m3:i64     = @index(g9, d1);

        // return
        z0:sym     = (`l_orderkey,`revenue,`o_orderdate,`o_shippriority):sym;
        z1:list<?> = @list(m0,m1,m2,m3);
        z:table    = @table(z0,z1);
        return z;
   }
}
```

