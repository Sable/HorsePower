# TPC-H Query 4

## SQL

```sql
  select
    o_orderpriority,
    count(*) as order_count
  from
    orders
  where
    o_orderdate >= date '1993-07-01'
    and o_orderdate < date '1993-07-01' + interval '3' month
    and exists (
      select
        *   
      from
        lineitem
      where
        l_orderkey = o_orderkey
        and l_commitdate < l_receiptdate
    )
  group by
    o_orderpriority
  order by
    o_orderpriority;
```

## HorseIR

```
import Bultin;

module default{
    import Builtin.*;
    def exists() : bool {
        a0:table = @load_table(`orders);
        a1:table = @load_table(`lineitem);
        t0:i64   = check_cast(@column_value(a0, `o_orderkey)    , i64);
        t1:enum  = check_cast(@column_value(a1, `l_orderkey)    , enum);
        t2:d     = check_cast(@column_value(a1, `l_commitdate)  , d  );
        t3:d     = check_cast(@column_value(a1, `l_receiptdate) , d  );

        w0:bool  = @lt(t2,t3);
        w1:enum  = @compress(w0,t1);
        w2:i64   = @values(w1);   // get internal indices
        w3:i64   = @keys(w1);
        w4:i64   = @len(w3);
        w5:bool  = @vector(w4, 0:bool);
        w6:i64   = index_assign(w5, w2, 1:bool);
        return w5;
    }
    def main() : table{
        // step 0: load table
        a0:table = @load_table(`orders);
        t0:d     = check_cast(@column_value(a0, `o_orderdate)    , d  );
        t1:sym   = check_cast(@column_value(a0, `o_orderpriority), sym);

        // step 1: where clause
        w0 :bool = @geq(t0,1993.07.01:d);
        w1 :d    = @datetime_add(1993.07.01:d,3:i64,`month);
        w2 :bool = @lt(t0,w1);
        w3 :bool = @and(w0,w2);
        w4 :bool = @exists();
        w5 :bool = @and(w3,w4);

        // step 2: group by
        g0:sym   = @compress(w5,t1);
        g1:dict<i64, list<i64>> = @group(g0);

        // step 3: select
        s0:i64       = @keys(g1);
        s1:list<i64> = @values(g1);
        s2:list<i64> = @each(@len,s1);
        s3:i64       = @raze(s2);
        s4:sym       = @index(g0,s0);

        // step 4: order by
        r0:i64       = @order(s4, 1:bool);

        // step 5: materialization
        m0:sym       = (`o_orderpriority,`order_count):sym;
        m1:list<sym> = @to_list(m0);
        m2:sym       = @index(s4,r0);
        m3:i64       = @index(s3,r0);
        m4:list<?>   = @list(m2,m3);

        z:table = @table(m1,m4);
        return z;
   }
}
```

