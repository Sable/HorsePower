# TPC-H Query 16

## SQL

```sql
  select
    p_brand,
    p_type,
    p_size,
    count(distinct ps_suppkey) as supplier_cnt
  from
    partsupp,
    part
  where
    p_partkey = ps_partkey
    and p_brand <> 'Brand#45'
    and p_type not like 'MEDIUM POLISHED%'
    and p_size in (49, 14, 23, 45, 19, 3, 36, 9)
    and ps_suppkey not in (
      select
        s_suppkey
      from
        supplier
      where
        s_comment like '%Customer%Complaints%'
    )
  group by
    p_brand,
    p_type,
    p_size
  order by
    supplier_cnt desc,
    p_brand,
    p_type,
    p_size;
```

## HorseIR

```
module default{
    import Bultin;

    def simulateQ16_sub() : i64 {
        a0:table = @load_table(`supplier);
        t0:str = check_cast(@column_value(a0, `s_comment), str);
        t1:i64 = check_cast(@column_value(a0, `s_suppkey), i64);
        w0:bool= @like(t0, "%Customer%Complaints%");
        w1:i64 = @compress(w0, t1);
        return w1;
    }

    def main() : table{
        // step 0: load table
        a0:table = @load_table(`partsupp);
        a1:table = @load_table(`part);

        t0:sym = check_cast(@column_value(a1, `p_brand)   , sym);
        t1:sym = check_cast(@column_value(a1, `p_type)    , sym);
        t2:i64 = check_cast(@column_value(a1, `p_size)    , i64);
        t3:i64 = check_cast(@column_value(a1, `p_partkey) , i64);
        t4:enum = check_cast(@column_value(a0, `ps_partkey), enum);
        t5:i64 = check_cast(@column_value(a0, `ps_suppkey), i64);

        // step 1: where clause
        w0:bool = @neq(t0, `"Brand#45");
        w1:bool = @like(t1, "MEDIUM POLISHED%");
        w2:bool = @not(w1);
        c0:i64  = (49, 14, 23, 45, 19, 3, 36, 9):i64;
        w3:bool = @member(c0, t2);
        sub:i64 = simulateQ16_sub();
        w4:bool = @member(sub, t5);
        w5:bool = @not(w4);

        w6:bool = @and(w0, w2);
        w7:bool = @and(w6, w3);

        // join
        w8:i64   = @values(t4);
        w9:i64   = @compress(w5, w8);
        w10:bool = @index(w7, w9);
        w11:i64  = @compress(w10, w9);
        w12:i64  = @where(w5);
        w13:i64  = @compress(w10, w12);

        g0:sym = @index(t0, w11);
        g1:sym = @index(t1, w11);
        g2:i64 = @index(t2, w11);
        g3:i64 = @index(t5, w13);

        g4:list<?> = @list(g0, g1, g2);
        g5:dict<i64, list<i64>> = @group(g4);
        g6:list<i64> = @values(g5);
        g7:i64 = @keys(g5);

        f0:list<i64> = @each_right(g3, g6, @index);
        f1:list<i64> = @each(f0, @unique);
        g8:list<i64> = @each(f1, @len);
        g9:i64       = @raze(g8);

        g10:sym = @index(g0, g7); // p_brand
        g11:sym = @index(g1, g7); // p_type
        g12:i64 = @index(g2, g7); // p_size

        d0:list<?> = @list(g9, g10, g11, g12);
        c1:bool    = (0,1,1,1):bool;
        d1:i64     = @orderby(d0, c1);

        // materialization
        m0:sym = @index(g10, d1);
        m1:sym = @index(g11, d1);
        m2:i64 = @index(g12, d1);
        m3:i64 = @index(g9 , d1);

        z0:sym     = (`p_brand, `p_type, `p_size, `supplier_cnt):sym;
        z1:list<?> = @list(m0,m1,m2,m3);
        z:table    = @table(z0,z1);
        return z;
   }
}
```

