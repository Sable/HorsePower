# TPC-H Query 12

## SQL

```sql
  select
    l_shipmode,
    sum(case
      when o_orderpriority = '1-URGENT'
        or o_orderpriority = '2-HIGH'
        then 1
      else 0
    end) as high_line_count,
    sum(case
      when o_orderpriority <> '1-URGENT'
        and o_orderpriority <> '2-HIGH'
        then 1
      else 0
    end) as low_line_count
  from
    orders,
    lineitem
  where
    o_orderkey = l_orderkey
    and l_shipmode in ('MAIL', 'SHIP')
    and l_commitdate < l_receiptdate
    and l_shipdate < l_commitdate
    and l_receiptdate >= date '1994-01-01'
    and l_receiptdate < date '1994-01-01' + interval '1' year
  group by
    l_shipmode
  order by
    l_shipmode;
```

## HorseIR

```
import Bultin;

module default{
    import Builtin;
    def main() : table{
        // step 0: load table
        a0:table = @load_table(`orders);
        a1:table = @load_table(`lineitem);
        t0:i64 = check_cast(@column_value(a0, `o_orderkey)     , i64);
        t1:sym = check_cast(@column_value(a0, `o_orderpriority), sym);
        t2:i64 = check_cast(@column_value(a1, `l_orderkey)     , i64);
        t3:sym = check_cast(@column_value(a1, `l_shipmode)     , sym);
        t4:d   = check_cast(@column_value(a1, `l_commitdate)   , d  );
        t5:d   = check_cast(@column_value(a1, `l_receiptdate)  , d  );
        t6:d   = check_cast(@column_value(a1, `l_shipdate)     , d  );

        // step 1: where clause
        j0:enum = @enum(t0,t2); // join
        w0:bool = @member((`MAIL,`SHIP):sym, t3);
        w1:bool = @lt(t4,t5);
        w2:bool = @lt(t6,t4);
        w3:bool = @geq(t5,1994.01.01:d);
        w4:d    = @datetime_add(1994.01.01:d,1,`year);
        w5:bool = @lt(t5,w4);
        w6:bool = @and(w0,w1);
        w7:bool = @and(w6,w2);
        w8:bool = @and(w7,w3);
        w9:bool = @and(w8,w5);
        w10:enum= @compress(w9,j0);
        w11:sym = @compress(w9,t3);
        w12:i64 = @to_index(j0);
        w13:i64 = @where(w9);
        w14:i64 = @index(w12,w13);

        // step 2: group by
        g0:dict<sym, list<i64>> = @group(w11);

        // step 3: select
        s0:list<sym> = @keys(g0);
        s1:list<i64> = @values(g0);
        s2:i64       = @raze(s1);
        s3:sym       = @index(t3,s2);         //l_shipmode
        w15:list<i64>= @each_right(@index,w14,s1);
        s4:list<sym> = @each_right(@index,t1,w15);
        s5:list<bool>= @each_left(@eq,s4,`1-URGENT);
        s6:list<bool>= @each_left(@eq,s4,`2-HIGH);
        s7:list<bool>= @each_item(@or,s5,s6);
        s8:list<i64> = @each(@sum,s7);        // high_line_count

        s9 :list<bool>= @each_left(@neq,s4,`1-URGENT);
        s10:list<bool>= @each_left(@neq,s4,`2-HIGH);
        s11:list<bool>= @each_item(@and,s9,s10);
        s12:list<i64> = @each(@sum,s11);      // low_line_count
        s13:i64       = @raze(s8);
        s14:i64       = @raze(s12);

        // step 4: order by
        r1:sym = @raze(s0);
        r0:i64 = @order(r1, 1:bool);

        // step 5: materialization
        m0:sym = @index(r1 ,r0);
        m1:i64 = @index(s13,r0);
        m2:i64 = @index(s14,r0);

        z0:sym = (`l_shipmode,`high_line_count,`low_line_count):sym
        z1:list<sym> = @tolist(z0);
        z2:list<?>   = @list(m0,m1,m2);
        z:table      = @table(z1,z2);
        return z;
   }
}
```

